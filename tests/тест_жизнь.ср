#влечь (
	"../б/ввыв/консоль/всё.ср"
)

фц __АСМ прервать() тлен (
_асм "
	искл рах рах
	быть вбайт (рбп -16) рах

	быть рах вбайт 50_000_000
	быть вбайт (рбп -8) рах

	быть еах 35
	задр рди (рбп -16)
	искл рси рси
	сзов
"
)


пусть очистка_консоли_и_перевод_в_начало: стр = ;;"\x1B[2J;;"\x1B[H",
;пусть очистка_консоли_и_перевод_в_начало: стр = "\x1B[2J\x1B[H",
пусть Эээ: стр = "Эээ world\n",

пусть жизнь: стр = "██",
пусть смерть: стр = "  ",
#вот размер 16
пусть поле: [[ч8 размер] размер] = [
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]

	[0 0 0 0    0 0 0 0    0 0 1 1    0 0 0 0]
	[0 0 1 0    0 0 0 0    0 1 0 1    0 0 0 0]
	[0 0 0 1    0 0 0 0    0 0 0 1    0 0 0 0]
	[0 1 1 1    0 0 0 0    0 0 0 0    0 0 0 0]

	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]

	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 1 1 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 1 1 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
],
пусть эволюция: [[ч8 размер] размер] = [],

фц вывести_поле() тлен (
	пусть ( х у: ч32 )
	вещать(очистка_консоли_и_перевод_в_начало),

	х => (0 ... размер) (
		у => (0 ... размер) (
			(поле[х][у] ? жизнь : смерть)
			|> вещать,
		)
		вещать..(Эээ),
	)
)

фц живых_рядом(х у: ч32) ч32 (
	пусть (ь ъ живых: ч32)
	живых = 0,

	ь => (-1 ..= 1) (
		ъ => (-1 ..= 1) (
			; если оба 0, то это сама клетка
			ь || ъ ?? (
				живых += поле
					[(х + ь + размер) % размер]
					[(у + ъ + размер) % размер],
			)
		)
	)
	воздать живых
)

фц пережить() тлен (
	пусть ( х у живых: ч32 )

	х => (0 ... размер) (
		у => (0 ... размер) (
			живых = живых_рядом(х у),
			эволюция[х][у] = поле[х][у]
				; если у живой клетки есть две или три живые соседки, то эта клетка продолжает жить
				; в противном случае (если живых соседей меньше двух или больше трёх) клетка умирает
				? живых == 2 || живых == 3
				; в пустой (мёртвой) клетке, с которой соседствуют три живые клетки, зарождается жизнь
				: живых == 3
			,
		)
	)
	; скопировать эволюцию в поле
	х => (0 ... размер) (
		у => (размер-1 ..= 0 назад) (
			поле[х][у] = эволюция[х][у],
		)
	)
)

пусть запятая: стр = ", ",
_асм "вход глав глав:"
фц глав(аргов: ч32 арги: *стр) тлен (
	;вещать..(10, запятая, 0, запятая, -123, запятая, Эээ),
	вечно (
		вывести_поле(),
		пережить(),
		прервать(),
	)
;;
	пусть ( а б в д е ё: ч32  г: стр )
	(а, б) = (б, а),
	(а, 0) = (б, а),
	(0, б) = (б, а),
	(0, 0) = (б, а),
	(а, 0, в, г, 0, е, ё) = че(),
	(а, 0, в, г, 0, е) = че2(),;;
)
;;
фц че() (ч8, ч16, ц32, стр, ч32, ц16, ц8) (
	воздать (1, 2, 3, 0, 5, 6, 7)
)
фц че2() (ч8, ч16, ц32, стр, ч32, ц16) (
	пусть ( а: ч32 аа: *ч32 б: ч64 )

	а = а + аа,
	а = аа + а,
	а = аа + б,
	а = б + аа,
	а = а + а,
	а = (аа + (2 + 1)) + 0,
	а = (аа + (2 + 1)) + 0,

	воздать (1, 2, 3, 0, 5, 6)
)
;;
