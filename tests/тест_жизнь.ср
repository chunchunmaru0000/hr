#влечь (
	"../б/ввыв/консоль/вещать.ср"
)

фц прервать() (
_асм "
	искл рах рах
	быть вбайт (рбп -16) рах

	быть рах вбайт 50_000_000
	быть вбайт (рбп -8) рах

	быть еах 35
	задр рди (рбп -16)
	искл рси рси
	сзов
"
)

пусть очистка_консоли_и_перевод_в_начало: стр = ;;"\x1B[2J;;"\x1B[H",
пусть Эээ: стр = "Эээ world\n",

пусть жизнь: стр = "██",
пусть смерть: стр = "__",
#вот размер 32
пусть поле: [[ч8 размер] размер] = [
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]

	[0 0 0 0    0 0 0 0    0 0 1 1    0 0 0 0]
	[0 0 1 0    0 0 0 0    0 1 0 1    0 0 0 0]
	[0 0 0 1    0 0 0 0    0 0 0 1    0 0 0 0]
	[0 1 1 1    0 0 0 0    0 0 0 0    0 0 0 0]

	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]

	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 1 1 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 1 1 0    0 0 0 0    0 0 0 0    0 0 0 0]
	[0 0 0 0    0 0 0 0    0 0 0 0    0 0 0 0]
],
пусть эволюция: [[ч8 размер] размер] = [],

фц вывести_поле() (
	пусть ( х у: ч32 )

	вещать(очистка_консоли_и_перевод_в_начало),

	х = 0, вечно (
		у = 0, вечно (
			(поле[х][у] ? жизнь : смерть)
			|> вещать,
		++у >= размер ?? (идти после_у) )после_у:
		вещать(Эээ),
	++х >= размер ?? (идти после_х) )после_х:
)

фц живых_рядом(х у: ч32) ч32 (
	пусть (ь ъ хь уъ живых: ч32)
	живых = 0,

	ь = -1, вечно (
		ъ = -1, вечно (
			; если оба 0, то это сама клетка
			!ь && !ъ ?? (идти пропустить_клетку)

			хь = х + ь, уъ = у + ъ,
			живых += поле
				[хь < 0 ? размер-1 : хь < размер ? хь : 0]
				[уъ < 0 ? размер-1 : уъ < размер ? уъ : 0]
			,
		пропустить_клетку:
		++ъ >= 2 ?? (идти после_ъ) )после_ъ:
	++ь >= 2 ?? (идти после_ь) )после_ь:

	; инструкции return(воздать) еще нет
	_асм "	быть еах чбайт(рбп живых)
"
)

фц пережить() (
	пусть ( х у живых: ч32 )

	х = 0, вечно (
		у = 0, вечно (
			живых = живых_рядом(х у),
			эволюция[х][у] = поле[х][у]
				; если у живой клетки есть две или три живые соседки, то эта клетка продолжает жить
				; в противном случае (если живых соседей меньше двух или больше трёх) клетка умирает
				? живых == 2 || живых == 3
				; в пустой (мёртвой) клетке, с которой соседствуют три живые клетки, зарождается жизнь
				: живых == 3
			,
		++у >= размер ?? (идти после_у) )после_у:
	++х >= размер ?? (идти после_х) )после_х:

	; скопировать эволюцию в поле
	х = 0, вечно (
		у = 0, вечно (
			поле[х][у] = эволюция[х][у],
		++у >= размер ?? (идти после_у2) )после_у2:
	++х >= размер ?? (идти после_х2) )после_х2:
)

_асм "вход глав глав:"
фц глав(аргов: ч32 арги: *стр) тлен (
	вечно (
		вывести_поле(),
		пережить(),
		прервать(),
	)
)
