#надо "../../память/пласт.ср"

;;
Приставка П значит пласт - Пласт Ряд
;;
лик ПРяд (
	нч: стр
	запас размер: ч32
)

#се рнч8  (# (окак  стр р->нч) #)
#се рнч32 (# (окак *ч32 р->нч) #)
#се рнч64 (# (окак *ч64 р->нч) #)

;#define p_last(l) (plist_get((l), (l)->size - 1))
;#define plat_plist(l, o) (plat((l), (o)->st, (o)->size))
фц новый_пряд(начальный_запас размер_звена: ц32) лик ПРяд (
	р есть лик ПРяд = пластом(мера &лик ПРяд + начальный_запас * размер_звена),
	р->запас = начальный_запас,
	р->размер = 0,
	р->нч = р + 1,
	воздать р,
)

фц добавить8(р: лик ПРяд смысл: ц8) тлен (
	р->размер >= р->запас ?? (
		р->нч = р->нч.пере_пласт(р->размер * мера ц8, (р->запас *= 2) * мера ц8),
	)
	рнч8[р->размер++] = смысл,
)
фц добавить32(р: лик ПРяд смысл: ч32) тлен (
	р->размер >= р->запас ?? (
		р->нч = р->нч.пере_пласт(р->размер * мера ч32, (р->запас *= 2) * мера ч32)
	)
	рнч32[р->размер++] = смысл,
)
фц __АСМ добавить64(р: лик ПРяд смысл: ч64) тлен ( _асм "
	идти добавить64__{ук_лик_ПРяд_ук_тлен_тлен}
")
фц добавить64(р: лик ПРяд смысл: *тлен) тлен (
	р->размер >= р->запас ?? (
		р->нч = р->нч.пере_пласт(р->размер * мера ч64, (р->запас *= 2) * мера ч64)
	)
	рнч64[р->размер++] = смысл,
)

фц звено8 (р: лик ПРяд место: ц32) ц8  ( воздать рнч8 [место] )
фц звено32(р: лик ПРяд место: ц32) ч32 ( воздать рнч32[место] )
фц звено64(р: лик ПРяд место: ц32) ч64 ( воздать рнч64[место] )

фц менять8 (р: лик ПРяд место: ц32 смысл: ц8)  тлен ( рнч8 [место] = смысл )
фц менять32(р: лик ПРяд место: ц32 смысл: ч32) тлен ( рнч32[место] = смысл )
фц менять64(р: лик ПРяд место: ц32 смысл: ч64) тлен ( рнч64[место] = смысл )

; ПРяд Добавить Раз
фц прдр8 (р: лик ПРяд из: *тлен раз: ц32) тлен (
	раз == 0 ?? ( воздать, )
	р->запас < р->размер + раз ?? (
		р->нч = р->нч.пере_пласт(
			мера ц8 *  р->запас,
			мера ц8 * (р->запас = р->запас * 2 + раз)
		)
	)
	из.вторить(рнч8 + р->размер, раз * мера ц8)
	р->размер += раз,
)
фц прдр32(р: лик ПРяд из: *тлен раз: ц32) тлен (
	раз == 0 ?? ( воздать, )
	р->запас < р->размер + раз ?? (
		р->нч = р->нч.пере_пласт(
			мера ч32 *  р->запас,
			мера ч32 * (р->запас = р->запас * 2 + раз)
		)
	)
	из.вторить(рнч32 + р->размер, раз * мера ч32)
	р->размер += раз,
)
фц прдр64(р: лик ПРяд из: *тлен раз: ц32) тлен (
	раз == 0 ?? ( воздать, )
	р->запас < р->размер + раз ?? (
		р->нч = р->нч.пере_пласт(
			мера ч64 *  р->запас,
			мера ч64 * (р->запас = р->запас * 2 + раз)
		)
	)
	из.вторить(рнч64 + р->размер, раз * мера ч64)
	р->размер += раз,
)
